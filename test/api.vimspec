Describe github_complete#api
    Describe #call_cached()
        It returns success for valid issue API call
            let res = github_complete#api#call_cached("repos/rhysd/clever-f.vim/issues", {})
            Assert IsList(res)
            Assert True(!empty(res))
            Assert True(has_key(res[0], 'number'))
            Assert True(has_key(res[0], 'title'))
        End

        It returns empty result for invalid GitHub API call
            let res = github_complete#api#call_cached("piyo/poyo/puyo", {})
            Assert True(empty(res))
        End

        It caches the result
            Assert True(empty(github_complete#api#get_cache("emojis")))
            let res = github_complete#api#call_cached("emojis", {})
            Assert IsDict(res)
            Assert True(!empty(res))
            Assert True(res['+1'] =~# '^https://')

            let answer = res['+1']
            let cached = github_complete#api#get_cache("emojis")

            Assert True(!empty(cached))
            Assert Equals(answer, cached['+1'])
        End
    End

    Describe #call_async_cached()
        It returns instantly and start fetching
            Skip because I don't know how to install dependant plugins yet.

            let res =github_complete#api#call_async_cached("licenses", {})
            Assert IsList(res)
            Assert True(empty(res))

            5sleep

            let res =github_complete#api#call_async_cached("licenses", {})
            Assert IsList(res)
            Assert True(!empty(res))
            Assert True(res[0].url =~# '^https://')
        End
    End
End
