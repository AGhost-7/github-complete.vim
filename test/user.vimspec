function! s:set_first_line(str)
    call setline(1, a:str)
endfunction

Describe 'github_complete#user'
    Describe #find_start()
        It returns the column to start completion before '@'
            call s:set_first_line(' aaa @bbb')
            normal! gg$
            Assert Equals(github_complete#user#find_start(' aaa @bbb'), 5)

            call s:set_first_line(' aa @bbb ccc')
            normal! gg$Tb
            Assert Equals(github_complete#user#find_start(' aa @bbb ccc'), 4)

            call s:set_first_line(' a @bbb)')
            normal! gg$Tb
            Assert Equals(github_complete#user#find_start(' a @bbb)'), 3)
        End

        It returns the column to start completion before github.com host
            call s:set_first_line(' aaa https://github.com/rhy')
            normal! gg$
            Assert Equals(github_complete#user#find_start(' aaa https://github.com/rhy'), 24)

            call s:set_first_line(' aa https://github.com/rhy bbb')
            normal! gg$Ty
            Assert Equals(github_complete#user#find_start(' aa https://github.com/rhy bbb'), 23)
        End

        It doesn't match except for other cases
            call s:set_first_line(' aaa bbb')
            normal! gg$
            Assert Equals(github_complete#user#find_start(' aaa bbb'), -1)
        End

        It doesn't match when no character is specified
            call s:set_first_line(' aaa @')
            normal! gg$
            Assert Equals(github_complete#user#find_start(' aaa @'), -1)
        End
    End

    Describe #candidates()
        It returns all matched '@' names
            let cs = github_complete#user#candidates('@rhysd')
            Assert IsList(cs)
            Assert True(!empty(cs))
            Assert True(cs[0].word =~# '^@\w\+$')
        End

        It returns all matched names after github.com host
            " Note: when github.com/foo is found, base will be 'foo'
            let cs = github_complete#user#candidates('rhysd')
            Assert IsList(cs)
            Assert True(!empty(cs))
            Assert True(cs[0].word =~# '^\w\+$')
        End

        It caches previous candidates
            call github_complete#api#reset_cache()
            Assert True(empty(github_complete#api#get_cache()))

            let cs = github_complete#user#candidates('@supermomonga')

            Assert True(github_complete#api#is_cached('search/users', 'q=supermomonga+in:login'))
            Assert Equals(cs, github_complete#user#candidates('@supermomonga'))
        End

    End
End
